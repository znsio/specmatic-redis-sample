plugins {
    id 'java-library'
    id 'maven-publish'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
//        url = file('<path to Specmatic Redis extracted repo>/repo')
        url -> throw new GradleException("Please set the URL of the local repository as shown in the line above")
    }
}

dependencies {
    api libs.org.springframework.boot.spring.boot.starter.data.redis
    api libs.org.springframework.boot.spring.boot.starter.web
    api libs.org.springframework.boot.spring.boot.starter.actuator
    testImplementation libs.org.springframework.boot.spring.boot.starter.test
    testImplementation "io.specmatic:junit5-support:2.7.0"
    testImplementation "io.specmatic.redis:specmatic-redis:0.9.2:original"
}

group = 'io.specmatic'
version = '0.0.1-SNAPSHOT'
description = 'Redis Stub Sample'

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform {
        includeEngines("junit-jupiter")
    }
    testLogging.showStandardStreams = true

    afterSuite { desc, result ->
        if (!desc.parent) {
            def resultLine = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            def lineLength = resultLine.length()
            def separator = "-" * lineLength

            println separator
            println resultLine
            println separator
        }
    }
}

configurations {
    all {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        exclude group: 'ch.qos.logback', module: 'logback-classic'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
    }
}